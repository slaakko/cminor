// =================================
// Copyright (c) 2017 Seppo Laakko
// Distributed under the MIT license
// =================================

[hpp]#include <cminor/ast/CompileUnit.hpp>
[hpp]#include <cminor/parser/ParsingContext.hpp>
[cpp]#include <cminor/parser/Identifier.hpp>
[cpp]#include <cminor/parser/Function.hpp>
[cpp]#include <cminor/parser/Class.hpp>
[cpp]#include <cminor/parser/Interface.hpp>
[cpp]#include <cminor/parser/Enumeration.hpp>
[cpp]#include <cminor/parser/Constant.hpp>
[cpp]#include <cminor/parser/Delegate.hpp>

namespace cminor.parser
{
    [hpp]using namespace cminor::ast;

    grammar CompileUnitGrammar
    {
        using stdlib.spaces_and_comments;
        using IdentifierGrammar.Identifier;
        using IdentifierGrammar.QualifiedId;
        using FunctionGrammar.Function;
        using ClassGrammar.Class;
        using InterfaceGrammar.Interface;
        using EnumerationGrammar.EnumType;
        using ConstantGrammar.Constant;
        using DelegateGrammar.Delegate;
        using DelegateGrammar.ClassDelegate;
        skip spaces_and_comments;

        CompileUnit(ParsingContext* ctx) : CompileUnitNode*
            ::= empty{ value = new CompileUnitNode(span, fileName); }
                NamespaceContent(ctx, value->GlobalNs())
            ;

        NamespaceContent(ParsingContext* ctx, NamespaceNode* ns)
            ::= UsingDirectives(ctx, ns)
                Definitions(ctx, ns)
            ;

        UsingDirectives(ParsingContext* ctx, NamespaceNode* ns)
            ::= UsingDirective(ctx, ns)*
            ;

        UsingDirective(ParsingContext* ctx, NamespaceNode* ns)
            ::= UsingAliasDirective{ ns->AddMember(UsingAliasDirective); }
            |   UsingNamespaceDirective{ ns->AddMember(UsingNamespaceDirective); }
            ;

        UsingAliasDirective(var std::unique_ptr<IdentifierNode> id) : Node*
            ::= (keyword("using") Identifier{ id.reset(Identifier); } '=' QualifiedId! ';'!){ value = new AliasNode(span, id.release(), QualifiedId); }
            ;

        UsingNamespaceDirective : Node*
            ::= (keyword("using") QualifiedId! ';'!){ value = new NamespaceImportNode(span, QualifiedId); }
            ;

        Definitions(ParsingContext* ctx, NamespaceNode* ns)
            ::= (Definition(ctx, ns){ ns->AddMember(Definition); })*
            ;

        Definition(ParsingContext* ctx, NamespaceNode* ns) : Node*
            ::= NamespaceDefinition(ctx, ns){ value = NamespaceDefinition; }
            |   FunctionDefinition(ctx){ value = FunctionDefinition; }
            |   ClassDefinition(ctx){ value = ClassDefinition; }
            |   InterfaceDefinition(ctx){ value = InterfaceDefinition; }
            |   EnumTypeDefinition(ctx){ value = EnumTypeDefinition; }
            |   ConstantDefinition(ctx){ value = ConstantDefinition; }
            |   DelegateDefinition(ctx){ value = DelegateDefinition; }
            |   ClassDelegateDefinition(ctx){ value = ClassDelegateDefinition; }
            ;

        NamespaceDefinition(ParsingContext* ctx, NamespaceNode* ns) : NamespaceNode*
            ::= (keyword("namespace") QualifiedId!){ value = new NamespaceNode(span, QualifiedId); } '{'! NamespaceContent(ctx, value)! '}'!
            ;

        FunctionDefinition(ParsingContext* ctx) : FunctionNode*
            ::= Function(ctx){ value = Function; }
            ;

        ClassDefinition(ParsingContext* ctx) : ClassNode*
            ::= Class(ctx){ value = Class; }
            ;

        InterfaceDefinition(ParsingContext* ctx) : InterfaceNode*
            ::= Interface(ctx){ value = Interface; }
            ;

        EnumTypeDefinition(ParsingContext* ctx) : EnumTypeNode*
            ::= EnumType(ctx){ value = EnumType; }
            ;

        ConstantDefinition(ParsingContext* ctx) : ConstantNode*
            ::= Constant(ctx){ value = Constant; }
            ;

        DelegateDefinition(ParsingContext* ctx) : Node*
            ::= Delegate(ctx){ value = Delegate; }
            ;

        ClassDelegateDefinition(ParsingContext* ctx) : Node*
            ::= ClassDelegate(ctx){ value = ClassDelegate; }
            ;
    }
}
