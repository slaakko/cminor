// =================================
// Copyright (c) 2016 Seppo Laakko
// Distributed under the MIT license
// =================================

[hpp]#include <cminor/ast/Statement.hpp>
[hpp]#include <cminor/parser/ParsingContext.hpp>
[cpp]#include <cminor/parser/Keyword.hpp>
[cpp]#include <cminor/parser/Expression.hpp>
[cpp]#include <cminor/parser/TypeExpr.hpp>
[cpp]#include <cminor/parser/Identifier.hpp>

namespace cminor.parser
{
    [hpp]using namespace cminor::ast;

    grammar StatementGrammar
    {
        using stdlib.identifier;
        using KeywordGrammar.Keyword;
        using ExpressionGrammar.Expression;
        using TypeExprGrammar.TypeExpr;
        using IdentifierGrammar.Identifier;
        using ExpressionGrammar.ArgumentList;

        Statement(ParsingContext* ctx) : StatementNode*
            ::= LabeledStatement(ctx){ value = LabeledStatement; }
            |   ControlStatement(ctx){ value = ControlStatement; }
            |   IncrementStatement(ctx){ value = IncrementStatement; }
            |   DecrementStatement(ctx){ value = DecrementStatement; }
            |   ExpressionStatement(ctx){ value = ExpressionStatement; }
            |   AssignmentStatement(ctx){ value = AssignmentStatement; }
            |   ConstructionStatement(ctx){ value = ConstructionStatement; }
            |   EmptyStatement(ctx){ value = EmptyStatement; }
            |   ThrowStatement(ctx){ value = ThrowStatement; }
            |   TryStatement(ctx){ value = TryStatement; }
            ;

        LabelId : std::string
            ::= (identifier - Keyword){ value = identifier; }
            ;

        Label(var std::string label) : LabelNode*
            ::= (LabelId{ label = LabelId; } ':'){ value = new LabelNode(span, label); }
            ;

        LabeledStatement(ParsingContext* ctx) : StatementNode*
            ::= Label:lbl Statement(ctx):stmt{ stmt->SetLabelNode(lbl); value = stmt; }
            ;

        ControlStatement(ParsingContext* ctx) : StatementNode*
            ::= CompoundStatement(ctx){ value = CompoundStatement; }
            |   ReturnStatement(ctx){ value = ReturnStatement; }
            |   IfStatement(ctx){ value = IfStatement; }
            |   WhileStatement(ctx){ value = WhileStatement; }
            |   DoStatement(ctx){ value = DoStatement; }
            |   ForStatement(ctx){ value = ForStatement; }
            |   ForEachStatement(ctx){ value = ForEachStatement; }
            |   BreakStatement(ctx){ value = BreakStatement; }
            |   ContinueStatement(ctx){ value = ContinueStatement; }
            |   SwitchStatement(ctx){ value = SwitchStatement; }
            |   GotoCaseStatement(ctx){ value = GotoCaseStatement; }
            |   GotoDefaultStatement(ctx){ value = GotoDefaultStatement; }
            ;

        CompoundStatement(ParsingContext* ctx) : CompoundStatementNode*
            ::= (   '{'{ value = new CompoundStatementNode(span); value->SetBeginBraceSpan(span); }
                    (Statement(ctx):stmt{ value->AddStatement(stmt); })*
                    '}'!{ value->SetEndBraceSpan(span); }
                ){ value->GetSpan().SetEnd(span.End()); }
            ;

        ReturnStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("return") Expression(ctx)? ';'!){ value = new ReturnStatementNode(span, Expression); }
            ;

        IfStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("if") '(' Expression(ctx) ')' Statement(ctx):thens (keyword("else") Statement(ctx):elses)?){ value = new IfStatementNode(span, Expression, thens, elses); }
            ;

        WhileStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("while") '('! Expression(ctx)! ')'! Statement(ctx)!){ value = new WhileStatementNode(span, Expression, Statement); }
            ;

        DoStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("do") Statement(ctx)! keyword("while")! '('! Expression(ctx)! ')'! ';'!){ value = new DoStatementNode(span, Statement, Expression); }
            ;

        ForStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("for") '(' ForInitStatement(ctx)! Expression(ctx)? ';'! ForLoopExpressionStatement(ctx)! ')'! Statement(ctx)!)
            { 
                value = new ForStatementNode(span, ForInitStatement, Expression, ForLoopExpressionStatement, Statement);
            }
            ;

        ForInitStatement(ParsingContext* ctx) : StatementNode*
            ::= AssignmentStatement(ctx){ value = AssignmentStatement; }
            |   ConstructionStatement(ctx){ value = ConstructionStatement; }
            |   EmptyStatement(ctx){ value = EmptyStatement; }
            ;

        ForLoopExpressionStatement(ParsingContext* ctx) : StatementNode*
            ::= IncrementExpressionStatement(ctx){ value = IncrementExpressionStatement; }
            |   DecrementExpressionStatement(ctx){ value = DecrementExpressionStatement; }
            |   AssignmentExpressionStatement(ctx){ value = AssignmentExpressionStatement; }
            |   empty{ value = new EmptyStatementNode(span); }
            ;

        ForEachStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("foreach") '('! TypeExpr(ctx)! Identifier! keyword("in")! Expression(ctx)! ')'! CompoundStatement(ctx))
            { 
                value = new ForEachStatementNode(span, TypeExpr, Identifier, Expression, CompoundStatement); 
            }
            ;

        BreakStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("break") ';'!){ value = new BreakStatementNode(span); }
            ;

        ContinueStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("continue") ';'!){ value = new ContinueStatementNode(span); }
            ;

        SwitchStatement(ParsingContext* ctx) : SwitchStatementNode*
            ::= (keyword("switch") '('! Expression(ctx)! ')'!){ value = new SwitchStatementNode(span, Expression); }
                '{'! (CaseStatement(ctx){ value->AddCase(CaseStatement); } | DefaultStatement(ctx){ value->SetDefault(DefaultStatement); })* '}'!
            ;

        CaseStatement(ParsingContext* ctx, var std::unique_ptr<CaseStatementNode> caseS) : CaseStatementNode*
            ::= ((empty{ caseS.reset(new CaseStatementNode(span)); } (keyword("case") Expression(ctx){ caseS->AddCaseExpr(Expression); } ':'!)+){ caseS->GetSpan().SetEnd(span.End()); }
                (Statement(ctx){ caseS->AddStatement(Statement); })*){ value = caseS.release(); }
            ;

        DefaultStatement(ParsingContext* ctx) : DefaultStatementNode*
            ::= (keyword("default") ':'!){ value = new DefaultStatementNode(span); }
                (Statement(ctx){ value->AddStatement(Statement); })*
            ;

        GotoCaseStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("goto") keyword("case") Expression(ctx)! ';'!){ value = new GotoCaseStatementNode(span, Expression); }
            ;

        GotoDefaultStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("goto") keyword("default") ';'!){ value = new GotoDefaultStatementNode(span); }
            ;

        AssignmentExpressionStatement(ParsingContext* ctx, var std::unique_ptr<Node> targetExpr) : StatementNode*
            ::= empty{ ctx->PushParsingLvalue(true); } (Expression(ctx):target{ ctx->PopParsingLvalue(); targetExpr.reset(target); } / { ctx->PopParsingLvalue(); } '=' Expression(ctx):source!)
                {
                    value = new AssignmentStatementNode(span, targetExpr.release(), source);
                }
            ;

        AssignmentStatement(ParsingContext* ctx) : StatementNode*
            ::= (AssignmentExpressionStatement(ctx) ';'!){ value = AssignmentExpressionStatement; value->GetSpan().SetEnd(span.End()); }
            ;

        ConstructionStatement(ParsingContext* ctx) : StatementNode*
            ::= TypeExpr(ctx) Identifier!{ value = new ConstructionStatementNode(span, TypeExpr, Identifier); }
                ('(' ArgumentList(ctx, value) ')'! | '=' Expression(ctx)!{ value->AddArgument(Expression); })? ';'!{ value->GetSpan().SetEnd(span.End()); }
            ;

        IncrementExpressionStatement(ParsingContext* ctx, var std::unique_ptr<Node> expr) : StatementNode*
            ::= 
            (   ("++" Expression(ctx):left!){ value = new IncrementStatementNode(span, left); } 
            |   (Expression(ctx):right{ expr.reset(right); } "++"){ value = new IncrementStatementNode(span, expr.release()); }
            ) 
            ;

        IncrementStatement(ParsingContext* ctx) : StatementNode*
            ::= (IncrementExpressionStatement(ctx) ';'!){ value = IncrementExpressionStatement; value->GetSpan().SetEnd(span.End()); }
            ;

        DecrementExpressionStatement(ParsingContext* ctx, var std::unique_ptr<Node> expr) : StatementNode*
            ::= 
            (   ("--" Expression(ctx):left!){ value = new DecrementStatementNode(span, left); } 
            |   (Expression(ctx):right{ expr.reset(right); } "--"){ value = new DecrementStatementNode(span, expr.release()); }
            )
            ;

        DecrementStatement(ParsingContext* ctx) : StatementNode*
            ::= (DecrementExpressionStatement(ctx) ';'!){ value = DecrementExpressionStatement; value->GetSpan().SetEnd(span.End()); }
            ;

        ExpressionStatement(ParsingContext* ctx, var std::unique_ptr<Node> expr) : StatementNode*
            ::= (Expression(ctx){ expr.reset(Expression); } ';'){ value = new ExpressionStatementNode(span, expr.release()); }
            ;

        EmptyStatement(ParsingContext* ctx) : StatementNode*
            ::= ';'{ value = new EmptyStatementNode(span); } 
            ;

        ThrowStatement(ParsingContext* ctx) : StatementNode*
            ::= (keyword("throw") Expression(ctx)? ';'!){ value = new ThrowStatementNode(span, Expression); }
            ;

        TryStatement(ParsingContext* ctx) : TryStatementNode*
            ::= keyword("try") CompoundStatement(ctx):tryBlock{ value = new TryStatementNode(span, tryBlock); } 
                ((Catch(ctx){ value->AddCatch(Catch); })+ (Finally(ctx){ value->SetFinally(Finally); })? | Finally(ctx):fin{ value->SetFinally(fin); })
            ;

        Catch(ParsingContext* ctx) : CatchNode*
            ::= (keyword("catch") '('! TypeExpr(ctx)! Identifier! ')'! CompoundStatement(ctx):catchBlock1){ value = new CatchNode(span, TypeExpr, Identifier, catchBlock1); }
            |   (keyword("catch") CompoundStatement(ctx):catchBlock2){ value = new CatchNode(span, catchBlock2); }
            ;

        Finally(ParsingContext* ctx) : CompoundStatementNode*
            ::= (keyword("finally") CompoundStatement(ctx)){ value = CompoundStatement; }
            ;
    }
}
