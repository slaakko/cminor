// =================================
// Copyright (c) 2017 Seppo Laakko
// Distributed under the MIT license
// =================================

[cpp]#include <cminor/cpg/cpgsyntax/Element.hpp>
[cpp]#include <cminor/cpg/cpgsyntax/Composite.hpp>
[hpp]#include <cminor/pl/Rule.hpp>

namespace cpg.syntax
{
    grammar RuleGrammar
    {
        using ElementGrammar.Identifier;
        using ElementGrammar.Signature;
        using CompositeGrammar.Alternative;
        using stdlib.string;

        Rule(cminor::parsing::Scope* enclosingScope): cminor::parsing::Rule*
                    ::= RuleHeader(enclosingScope){ value = RuleHeader; } "::=" RuleBody(value)
                    ;

        RuleHeader(cminor::parsing::Scope* enclosingScope): cminor::parsing::Rule*
                    ::= Identifier:name{value = new Rule(name, enclosingScope); value->SetSpan(span); } Signature(value)
                    ;

        RuleBody(cminor::parsing::Rule* rule)
                    ::= (Alternative(rule->GetScope()):definition! ';'!){ rule->SetDefinition(definition); }
                    ;
    }
}
