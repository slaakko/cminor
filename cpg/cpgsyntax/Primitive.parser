/*========================================================================
    Copyright (c) 2012-2016 Seppo Laakko
    http://sourceforge.net/projects/cmajor/
 
    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

 ========================================================================*/

[hpp]#include <../pl/Primitive.hpp>
[hpp]#include <../pl/Keyword.hpp>
[cpp]#include <cpgsyntax/Element.hpp>

namespace cpg.syntax
{
    grammar PrimitiveGrammar
    {
        using stdlib.char;
        using stdlib.string;
        using stdlib.escape;
        using ElementGrammar.QualifiedId;
        using ElementGrammar.StringArray;

        Primitive: cminor::parsing::Parser*
                        ::= Char{ value = Char; }
                        |   String{ value = String; }
                        |   CharSet{ value = CharSet; }
                        |   Keyword{ value = Keyword; }
                        |   KeywordList{ value = KeywordList; }
                        |   Empty{ value = Empty; }
                        |   Space{ value = Space; }
                        |   AnyChar{ value = AnyChar; }
                        |   Letter{ value = Letter; }
                        |   Digit{ value = Digit; }
                        |   HexDigit{ value = HexDigit; }
                        |   Punctuation{ value = Punctuation; }
                        ;

        Char: cminor::parsing::Parser*
                        ::= char:chr{ value = new CharParser(chr); }
                        ;

        String: cminor::parsing::Parser*
                        ::= string:str{ value = new StringParser(str); }
                        ;

        CharSet(var bool inverse, var std::string s): cminor::parsing::Parser*
                        ::= token
                            (   '['
                                ('^'{ inverse = true; })? 
                                (CharSetRange{ s.append(CharSetRange); })*
                                ']'!
                            )
                            {
                                value = new CharSetParser(s, inverse);
                            }
                            ;

        CharSetRange: std::string
                        ::= CharSetChar:first 
                            ('-' CharSetChar:last
                                { 
                                    value.append(1, first);
                                    value.append(1, '-');
                                    value.append(1, last);
                                }
                            |   empty{ value.append(1, first); }
                            )
                            ;
                            
        CharSetChar: char   ::= [^\\\]]{ value = *matchBegin; }
                            |   escape{ value = escape; }
                            ;

        Keyword: cminor::parsing::Parser*
                            ::= keyword("keyword") '('! KeywordBody! ')'!{ value = KeywordBody; }
                            ;

        KeywordBody: cminor::parsing::Parser*
                            ::= string:str
                            (   ',' QualifiedId:continuation!{ value = new KeywordParser(str, continuation); }
                            |   empty{ value = new KeywordParser(str); }
                            )
                            ;

        KeywordList: cminor::parsing::Parser*
                            ::= keyword("keyword_list") '('! KeywordListBody! ')'!{ value = KeywordListBody; }
                            ;

        KeywordListBody(var std::vector<std::string> keywords): cminor::parsing::Parser*
                            ::= QualifiedId:selector! ','! StringArray(&keywords)!{ value = new KeywordListParser(selector, keywords); }
                            ;

        Empty: cminor::parsing::Parser*
                            ::= keyword("empty"){ value = new EmptyParser(); }
                            ;

        Space: cminor::parsing::Parser*
                            ::= keyword("space"){ value = new SpaceParser(); }
                            ;

        AnyChar: cminor::parsing::Parser*
                            ::= keyword("anychar"){ value = new AnyCharParser(); }
                            ;

        Letter: cminor::parsing::Parser*
                            ::= keyword("letter"){ value = new LetterParser(); }
                            ;

        Digit: cminor::parsing::Parser*
                            ::= keyword("digit"){ value = new DigitParser(); }
                            ;

        HexDigit: cminor::parsing::Parser*
                            ::= keyword("hexdigit"){ value = new HexDigitParser(); }
                            ;

        Punctuation: cminor::parsing::Parser*
                            ::= keyword("punctuation"){ value = new PunctuationParser(); }
                            ;
    }
}
